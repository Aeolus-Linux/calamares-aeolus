 pkgname=calamares
pkgver=3.2.32.1
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('x86_64')
license=(GPL)
url="https://github.com/Aeolus-Linux/calamares-aeolus.git"
license=('LGPL')
depends=('kconfig' 
         'kcoreaddons' 
         'kiconthemes' 
         'ki18n'
         'kio' 
         'solid' 
         'yaml-cpp' 
         'kpmcore>=3.3.0' 
         'squashfs-tools'
         'mkinitcpio-openswap'
         'boost-libs' 
         'ckbcomp' 
         'hwinfo' 
         'qt5-svg' 
         'polkit-qt5' 
         'gtk-update-icon-cache')

makedepends=('extra-cmake-modules' 
             'qt5-tools' 
             'qt5-translations' 
             'git' 
             'boost')


source+=("https://github.com/calamares/calamares/releases/download/v${pkgver}/calamares-${pkgver}.tar.gz"
         git+https://github.com/Aeolus-Linux/calamares-aeolus.git)

sha256sums=('b3e17f62edee123857ffc6786c0bbdf9c780ec3bc25ffdf0da2028668156dd36'
            'SKIP')

prepare() {
    cp -a ${srcdir}/calamares-aeolus/* ${srcdir}/calamares-${pkgver}
    rm -rf ${srcdir}/calamares-aeolus
    cd ${srcdir}/calamares-${pkgver}
    sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' CMakeLists.txt
}


build() {
    cd ${srcdir}/calamares-${pkgver}

    mkdir -p build
    cd build
        cmake .. \
              -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_INSTALL_LIBDIR=lib \
              -DWITH_PYTHONQT:BOOL=OFF \
              -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg dracut dracutlukscfg \
                              dummyprocess dummypython dummycpp \
                              dummypythonqt services-openrc services-systemd"
        make
}

package() {
    cd ${srcdir}/calamares-${pkgver}/build
    make DESTDIR="$pkgdir" install
    install -Dm644 "../calamares.desktop" "$pkgdir/usr/share/applications/calamares.desktop"
	install -Dm755 "../data/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
    install -Dm644 "../data/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
    chmod 750 "$pkgdir"/etc/polkit-1/rules.d

}